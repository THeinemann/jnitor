apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'cpp'
apply plugin: 'google-test-test-suite'

group = 'local'
version = '0.0.1-SNAPSHOT'

description = """jnitor - C++ wrapper generator to access Java classes via JNI"""

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

String javaHome = System.properties['java.home'] + "/.."

mainClassName = "local.jnitor.Jnitor"

model {
	components {
		integration(NativeLibrarySpec) {
			sources {
				cpp {
					source {			
						srcDir "build/generatedTestSources"
						include "**/*.cpp"
					}
					exportedHeaders {
		                srcDir "build/generatedTestSources"
		            }
	            }
            }
		}
	}
	
	testSuites {
		integrationTest(GoogleTestTestSuiteSpec) {
			testing $.components.integration
		}
	}
	
	binaries {
		all {
            if (toolChain in Gcc) {
            	cppCompiler.args "-I" + javaHome + "/include/", "-I" + javaHome+ "/include/linux"
            }
		}
	
		withType(GoogleTestTestSuiteBinarySpec) {
            if (toolChain in Gcc) {
            	String jniLibPath = javaHome + '/jre/lib/amd64/server'
            	
            	// Setting rpath in the linker argument is a bit hacky, but I deemed it acceptable,
            	// because we are only building a test binary that will not be distributed.
            	// TODO: If gradle supports setting the LD_LIBRARY_PATH for the linker, check if this is a better option
                linker.args "-lgtest", '-L' + jniLibPath, '-ljvm', '-Wl,-rpath=' + jniLibPath
            }
		}
	}
}

// The C++ source code depends on sources generated during the JUnit tests 
tasks.withType(CppCompile) {
	dependsOn test
}
 
// Ensure tests are executed before the binaries are bundled to a jar library
tasks.jar {
	dependsOn check
}

tasks.run {
	args = ['-outputDirectory', 'build/generatedRun/']
}

repositories {
        
     maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile group: 'org.freemarker', name: 'freemarker', version:'2.3.23'
    compile group: 'args4j', name: 'args4j', version:'2.33'
    testCompile group: 'junit', name: 'junit', version:'4.12'
}

jar {
  manifest {
    attributes(
      'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
      'Main-Class': mainClassName
    )
  }
}