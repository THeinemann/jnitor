apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'cpp'
apply plugin: 'google-test-test-suite'

group = 'local'
version = '0.0.1-SNAPSHOT'

description = """jnitor - C++ wrapper generator to access Java classes via JNI"""

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

mainClassName = "local.jnitor.Jnitor"

model {
	components {
		integration(NativeExecutableSpec) {
			sources {
				
			}
		}
	}
	
	testSuites {
		integrationTest(GoogleTestTestSuiteSpec) {
			testing $.components.integration
		}
	}
	
	binaries {
		withType(GoogleTestTestSuiteBinarySpec) {
            if (toolChain in Gcc) {
                linker.args "-lgtest"
            }
		}
	}
}

// The C++ source code depends on sources generated during the JUnit tests 
tasks.withType(CppCompile) {
	dependsOn test
}
 
// Ensure tests are executed before the binaries are bundled to a jar library
tasks.jar {
	dependsOn check
}

tasks.run {
	args = ['-outputDirectory', 'build/generatedRun/']
}

repositories {
        
     maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile group: 'org.freemarker', name: 'freemarker', version:'2.3.23'
    compile group: 'args4j', name: 'args4j', version:'2.33'
    testCompile group: 'junit', name: 'junit', version:'4.12'
}

jar {
  manifest {
    attributes(
      'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
      'Main-Class': mainClassName
    )
  }
}